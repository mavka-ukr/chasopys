# Як створити просте розширення Мавки?

> Ця інструкція на цей час не актуальна.

<subject>Мавку</subject>, ще з часів JavaScript версії, можна розширювати. Раніше, очевидно, за допомогою JavaScript. На
щастя, <subject>Мавка</subject> вже відійша від JS, тому сьогодні розширюватимемо за допомогою <subject>Цілі</subject>.

У цьому дописі я покажу як просто можна виконувати код на <subject>Цілі</subject> з <subject>Мавки</subject>. Але навіть
більше — код на C++ через <subject>Ціль</subject> з <subject>Мавки</subject>.

Отож, поїхали. Для цього нам будуть треба останні версії <subject>Мавки</subject>, <subject>Цілі</subject> та Clang++.
На момент написання
цього допису в мене
версія <subject>[Мавки 0.113.0](https://xn--80ae5bu9f.xn--80aaf6ah.xn--j1amh/%D0%B2%D0%B8%D0%BF%D1%83%D1%81%D0%BA-%D0%BC%D0%B0%D0%B2%D0%BA%D0%B8-0.113.0.html)</subject>,
версія <subject>[Цілі 0.35.0](https://xn--k1avt2b.xn--j1amh)</subject>
та Clang++ 18.

Також треба завантажити файл `РМв1.в`
(наприклад
з [GitHub-у](https://github.com/mavka-ukr/mavka/blob/main/%D0%B2%D0%B8%D0%B7%D0%BD%D0%B0%D1%87%D0%B5%D0%BD%D0%BD%D1%8F/%D0%A0%D0%9C%D0%B21.%D0%B2)).

Структура директорії проекту буде така:

```
розширення_мавки_сума/
├─ РМв1.в
├─ sum_mavka_extension.cpp
├─ розширення_мавки_сума.ll
├─ розширення_мавки_сума.so
├─ розширення_мавки_сума.ц
└─ тест_розширення_сума.м
```

Створюємо файл `sum_mavka_extension.cpp` з наступним вмістом:

```cpp
extern "C" double mavka_extension_sum(double a, double b) {
  return a + b;
}
```

Створюємо файл `розширення_мавки_сума.ц` з наступним вмістом:

```tsil
взяти визначення РМв1;

зовнішня дія mavka_extension_sum(a: double, b: double): double;

дія нативна_дія_сума(предмет_нативної_дії: адреса<РМв1::Предмет>, Р: адреса<РМв1::Розширення>, предмет_я: адреса<РМв1::Предмет>, кількість_аргументів: позитивне, аргументи: памʼять<адреса<РМв1::Предмет>>, іменовані_аргументи: адреса<РМв1::ІменованіАргументи>, дані: адреса): РМв1::Результат {
  змінна ціль знайдено_аргумент_а = ні;
  змінна ціль аргумент_а: адреса<РМв1::Предмет> = пусто;
  змінна ціль знайдено_аргумент_б = ні;
  змінна ціль аргумент_б: адреса<РМв1::Предмет> = пусто;
  якщо іменовані_аргументи != пусто {
    знайдено_аргумент_а = РМв1::знайти_іменований_аргумент(Р, іменовані_аргументи, РМв1::назва_з_т8(Р, "а"), аргумент_а::адреса);
    знайдено_аргумент_б = РМв1::знайти_іменований_аргумент(Р, іменовані_аргументи, РМв1::назва_з_т8(Р, "б"), аргумент_б::адреса);
  }
  якщо кількість_аргументів > 0 {
    якщо знайдено_аргумент_а == ні {
      знайдено_аргумент_а = так;
      аргумент_а = аргументи[0];
    }
    якщо кількість_аргументів > 1 {
      якщо знайдено_аргумент_б == ні {
        знайдено_аргумент_б = так;
        аргумент_б = аргументи[1];
      }
    }
  }
  якщо знайдено_аргумент_а == ні {
    вернути РМв1::падіння(Р, РМв1::створити_текст_з_ю8(Р, ю8"Пропущено аргумент \"а\""));
  }
  якщо знайдено_аргумент_б == ні {
    вернути РМв1::падіння(Р, РМв1::створити_текст_з_ю8(Р, ю8"Пропущено аргумент \"б\""));
  }
  якщо РМв1::перевірити_чи_предмет_є_числом(аргумент_а, Р) == ні {
    вернути РМв1::падіння(Р, РМв1::створити_текст_з_ю8(Р, ю8"Аргумент \"а\" має бути числом"));
  }
  якщо РМв1::перевірити_чи_предмет_є_числом(аргумент_б, Р) == ні {
    вернути РМв1::падіння(Р, РМв1::створити_текст_з_ю8(Р, ю8"Аргумент \"б\" має бути числом"));
  }
  ціль а = РМв1::отримати_значення_числа(аргумент_а, Р);
  ціль б = РМв1::отримати_значення_числа(аргумент_б, Р);
  ціль результат = mavka_extension_sum(а, б);
  вернути РМв1::успіх(Р, РМв1::створити_число(Р, результат));
}

зовнішня дія завантажити_РМв1(Р: адреса<РМв1::Розширення>): РМв1::Результат {
  ціль предмет_нативної_дії_сума = РМв1::створити_нативну_дію(Р, РМв1::назва_з_т8(Р, "сума"), нативна_дія_сума, пусто, пусто);
  вернути РМв1::успіх(Р, предмет_нативної_дії_сума);
}
```

Компілюємо shared object `розширення_мавки_сума.so` за допомогою наступних команд:

```shell
ціль розширення_мавки_сума.ll скомпілювати розширення_мавки_сума.ц
```

```shell
clang++ -shared -fPIC -o розширення_мавки_сума.so розширення_мавки_сума.ll sum_mavka_extension.cpp
```

Створюємо модуль <subject>Мавки</subject> `тест_розширення_сума.м`:

```мавка
взяти біб мавка

сума = мавка.завантажити_РМв1(ю"./розширення_мавки_сума.so")
друк(сума(2, 2))
```

Пробуємо:

```shell
мавка тест_розширення_сума.м
```

Має надрукувати `4`.

Отож, як видно, виконувати нативний код на <subject>Цілі</subject> чи C++ з <subject>Мавки</subject> дуже просто.
Головне розібратись з файлом `РМв1.в` та розуміти <subject>Ціль</subject>.

---

_Написав [Давид](https://кдб.укр). Опубліковано 01.12.2024. Оновлено 05.04.2025._