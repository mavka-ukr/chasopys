# Як створити просте розширення Мавки?

<subject>Мавку</subject>, ще з часів JavaScript версії, можна розширювати. Раніше, очевидно, за допомогою JavaScript. На
щастя, <subject>Мавка</subject> вже відійша від JS, тому сьогодні розширюватимемо за допомогою <subject>Цілі</subject>.

У цьому дописі я покажу як просто можна виконувати код на <subject>Цілі</subject> з <subject>Мавки</subject>. Але навіть
більше — код на C++ через <subject>Ціль</subject> з <subject>Мавки</subject>.

Отож, поїхали. Для цього нам будуть треба останні версії <subject>Мавки</subject>, <subject>Цілі</subject> та Clang++.
На момент написання
цього допису в мене
версія <subject>[Мавки 0.112.0](https://xn--80ae5bu9f.xn--80aaf6ah.xn--j1amh/%D0%B2%D0%B8%D0%BF%D1%83%D1%81%D0%BA-%D0%BC%D0%B0%D0%B2%D0%BA%D0%B8-0.112.0.html)</subject>,
версія <subject>[Цілі 0.29.0](https://xn--80ae5bu9f.xn--k1avt2b.xn--j1amh/%D0%B2%D0%B8%D0%BF%D1%83%D1%81%D0%BA-%D1%86%D1%96%D0%BB%D1%96-0.29.0.html)</subject>
та Clang++ 18.

Також треба завантажити файл `РМв1.в`
(наприклад з [GitHub-у](https://github.com/mavka-ukr/mavka/blob/main/%D0%A0%D0%9C%D0%B21/%D0%A0%D0%9C%D0%B21.%D0%B2)).

Структура директорії проекту буде така:

```
розширення_мавки_сума/
├─ РМв1.в
├─ sum_mavka_extension.cpp
├─ розширення_мавки_сума.ll
├─ розширення_мавки_сума.so
├─ розширення_мавки_сума.ц
└─ тест_розширення_сума.м
```

Створюємо файл `sum_mavka_extension.cpp` з наступним вмістом:

```cpp
extern "C" double mavka_extension_sum(double a, double b) {
  return a + b;
}
```

Створюємо файл `розширення_мавки_сума.ц` з наступним вмістом:

```tsil
взяти визначення РМв1;

зовнішня дія mavka_extension_sum(a: double, b: double): double;

дія нативна_дія_сума(обʼєкт_нативної_дії: адреса<РМв1::Обʼєкт>, Р: адреса<РМв1::Розширення>, обʼєкт_я: адреса<РМв1::Обʼєкт>, кількість_аргументів: позитивне, аргументи: адреса<адреса<РМв1::Обʼєкт>>, іменовані_аргументи: адреса, дані: адреса): РМв1::Результат {
  якщо кількість_аргументів > 1 {
    ціль обʼєкт_аргумента_1 = аргументи[0];
    ціль обʼєкт_аргумента_2 = аргументи[1];
    ціль а = РМв1::отримати_значення_числа(обʼєкт_аргумента_1, Р);
    ціль б = РМв1::отримати_значення_числа(обʼєкт_аргумента_2, Р);
    ціль результат = mavka_extension_sum(а, б);
    вернути РМв1::успіх(Р, РМв1::створити_число(Р, результат));
  }
  вернути РМв1::падіння(Р, РМв1::створити_текст_з_Ю8(Р, ю8"Недостатньо аргументів"));
}

зовнішня дія завантажити_РМв1(Р: адреса<РМв1::Розширення>): РМв1::Результат {
  ціль обʼєкт_нативної_дії_сума = РМв1::створити_нативну_дію(Р, РМв1::назва_з_Ю8(Р, ю8"сума"), нативна_дія_сума, пусто, пусто);
  вернути РМв1::успіх(Р, обʼєкт_нативної_дії_сума);
}
```

Компілюємо shared object `розширення_мавки_сума.so` за допомогою наступних команд:

```shell
ціль розширення_мавки_сума.ll скомпілювати розширення_мавки_сума.ц
```

```shell
clang++ -shared -o розширення_мавки_сума.so розширення_мавки_сума.ll sum_mavka_extension.cpp
```

Створюємо модуль <subject>Мавки</subject> `тест_розширення_сума.м`:

```мавка
взяти біб мавка

сума = мавка.завантажити_РМв1(ю"./розширення_мавки_сума.so")
друк(сума(2, 2))
```

Пробуємо:

```shell
мавка тест_розширення_сума.м
```

Має надрукувати `4`.

Отож, як видно, виконувати нативний код на <subject>Цілі</subject> чи C++ з <subject>Мавки</subject> дуже просто.
Головне розібратись з файлом `РМв1.в` та розуміти <subject>Ціль</subject>.

---

_Написав [Давид](https://кдб.укр). Опубліковано 01.12.2024._